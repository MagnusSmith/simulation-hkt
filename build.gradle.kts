plugins {
  id("java-library")
  id("maven-publish")
  id("jacoco")
  id("com.diffplug.spotless") version "6.25.0"
}

group = "org.higher-kinded-j"

version = project.findProperty("projectVersion")?.toString() ?: "v1.0.0-SNAPSHOT"

repositories { mavenCentral() }

dependencies {
  implementation("org.jspecify:jspecify:1.0.0")
  testImplementation(platform("org.junit:junit-bom:5.10.2"))
  testImplementation("org.junit.jupiter:junit-jupiter")
  testImplementation("org.assertj:assertj-core:3.25.3")
}

java {
  sourceCompatibility = JavaVersion.VERSION_24
  targetCompatibility = JavaVersion.VERSION_24
  withSourcesJar()
  withJavadocJar()
}

spotless {
  lineEndings = com.diffplug.spotless.LineEnding.UNIX

  java {
    // Apply to all Java files in src/main and src/test
    target("src/**/*.java")

    // Use Google Java Format
    googleJavaFormat("1.26.0")
        // .aosp() // Use AOSP style instead of Google Style (optional)
        // .style("AOSP") // Alternative way to specify AOSP style
        .reflowLongStrings() // Optional: Reflow long string literals
        .formatJavadoc(true) // Optional: default is true, set false to disable javadoc formatting

    removeUnusedImports()
    trimTrailingWhitespace()
    // endWithNewline() // Usually handled by googleJavaFormat

    // Optional: Add license header
    // licenseHeaderFile(rootProject.file("config/spotless/copyright.txt"),
    // "(package|import|public|@)")
  }

  // Optional: Configure formatting for Gradle files themselves
  kotlinGradle {
    target("*.gradle.kts", "settings.gradle.kts")
    ktfmt("0.47") // Use ktfmt for Kotlin files
  }
}

tasks.test {
  useJUnitPlatform()
  finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
  dependsOn(tasks.test)

  reports {
    xml.required.set(true)
    html.required.set(true)
    csv.required.set(false)
  }

  val mainSourceSet = sourceSets.main.get()
  val compiledClasses = mainSourceSet.output.classesDirs
  classDirectories.setFrom(
      files(
          compiledClasses.map { dir ->
            fileTree(dir).apply {
              exclude(
                  "**/example/**", // Exclude the example package
                  "**/*Kind.class", // Exclude Kind interfaces
                  "**/*Holder*.class" // Exclude internal holders
                  )
            }
          }))
  sourceDirectories.setFrom(files(mainSourceSet.allSource.srcDirs))
}
publishing {
  publications {
    // Create a publication named "mavenJava" (or choose your own name)
    create<MavenPublication>("mavenJava") {
      // Publish the components generated by the 'java' plugin (JAR, POM)
      from(components["java"])


      artifact(tasks.getByName("sourcesJar"))
      artifact(tasks.getByName("javadocJar"))



      pom {
        name.set("Higher-Kinded-J")
        description.set("Bringing Higher-Kinded Types to Java Functional Patterns")
        url.set("https://github.com/higher-kinded-j/higher-kinded-j")

        licenses {
          license {
            name.set("The MIT License")
            url.set("https://opensource.org/licenses/MIT")
          }
        }
        developers {
          developer {
            id.set("higher-kinded-j")
            name.set("Magnus Smith")
            email.set("simulation-hkt@gmail.com")
          }
        }
        scm {
          connection.set("scm:git:git://github.com/higher-kinded-j/higher-kinded-j.git")
          developerConnection.set("scm:git:ssh://github.com/higher-kinded-j/higher-kinded-j.git")
          url.set("https://github.com/higher-kinded-j/higher-kinded-j")
        }
      }
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/higher-kinded-j/higher-kinded-j")

      credentials {

        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}
